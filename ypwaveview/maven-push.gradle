apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.source
    exclude "**/R.class"
    exclude "**/BuildConfig.class"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // group id，发布后引用的依赖的 group id
            groupId 'com.yangpingapps'
            // 发布后引用的依赖的 artifact id
            artifactId 'ypwaveview'
            // 发布的版本
            version '1.0.4'
            // 发布的 arr 的文件和源码文件
            artifact("$buildDir/outputs/aar/ypwaveview-release.aar")
            artifact androidSourcesJar
            pom {
                packaging 'aar'
                // 构件名称，可以自定义
                name = 'ypwaveview'
                // 构件描述
                description = 'A Progress for View Like Wave on Android'
                // 构件主页
                url = 'https://github.com/ZihYangLin/YPWaveView'
                // 许可证名称和地址
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                // 开发者信息
                developers {
                    developer {
                        id = 'zihyang lin'
                        name = 'Zih-Yang Lin'
                        email = 'yangpingapps@gmail.com'
                    }
                }
                // 版本控制仓库地址
                scm {
                    url = 'https://github.com/ZihYangLin/YPWaveView'
                    connection = 'scm:git:github.com/ZihYangLin/YPWaveView.git'
                    developerConnection = 'scm:git:ssh://git@github.com/ZihYangLin/YPWaveView.git'
                }
            }
        }
    }
    repositories {
        maven {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())

            // 发布的位置，这里根据发布的版本区分了 SNAPSHOT 和最终版本两种情况
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                // 这里就是之前在 issues.sonatype.org 注册的账号
                username properties.getProperty("ossrhUsername")
                password properties.getProperty("ossrhPassword")
            }
        }
    }
}

signing {
    sign publishing.publications
}